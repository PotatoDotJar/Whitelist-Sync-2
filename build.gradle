buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

// Add shadow plugin
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    maven { url = 'https://maven.rmnad.net/releases' }
}

version = "${mc_version}-${mod_version}"
group = "${package_group}"
archivesBaseName = "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    runs {
        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args += ["nogui"]
            mods {
                whitelistsync2 {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    shadow
    implementation.extendsFrom(shadow)
}

// Add shadowed deps to runs
//minecraft.runs.all {
//    // Set the minecraft_classpath token to the paths of all jars in the library configuration
//    // This is added with the actual Minecraft classpath to get the real classpath information later on
//    lazyToken('minecraft_classpath') {
//        configurations.shadow.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
//    }
//}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Shade other dependencies into the final jar
    // These deps are now handled in the WhitelistSyncLib library
    //shadow "org.xerial:sqlite-jdbc:3.36.0.3"
    //shadow "mysql:mysql-connector-java:8.0.13"
    //shadow "org.postgresql:postgresql:42.2.23.jre7"
    shadow "net.rmnad.minecraft.forge:whitelistsynclib:${whitelistsynclib_version}"
}

// START: SHADING DEPENDENCIES
shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')
}

reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
// END: SHADING DEPENDENCIES

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}